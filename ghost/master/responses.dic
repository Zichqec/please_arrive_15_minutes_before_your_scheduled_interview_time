/*
	Possible intents:
	
	growl
	hiss
	screaming
	excited
	interested
	stressed
	thoughtful
	confused
	inquisitive
	hesitant
	repeating
	soft
	loud
	long
	short
	one word
	definitive
	demanding
	
	Repeated letter reference:
	
	aaa - screaming
	eee - excited (also triggered by multiple scattered !s)
	ooo - interested
	hhh - stressed
	mmm - thoughtful
	sss - hiss (also triggered by multiple scattered Hs)
	rrr - growl (also triggered by multiple scattered Gs)
	?? - confused
*/

//User's response to Boss asking a question
OnAnswerQuestion
{
	_input = TOLOWER(reference0)
	_intents = ProcessQuestion(_input)
	
	//This can be set up like the if/else structure in OnAskQuestion below
	if Intent("soft,growl",_intents)
	{
		"oh is that a threat? hmmmmmmmm"
	}
	elseif Intent("screaming,loud",_intents)
	{
		"AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA WHY ARE WE SCREAMING AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA"
	}
	elseif "gold" _in_ _input
	{
		"gold!!! my gold"
	}
	elseif _input == ""
	{
		"oh, uh, nothing to say, oh no"
	}
	else //Didn't match any intents
	{
		"Mysterious..."
	}
}

//Resoponses to yes/no questions
OnAnswerYesNo
{
	"\0\b[0]"
	--
	if reference1 == "worked with dragon"
	{
		if reference0 == "yes"
		{
			"\0\b[4]%(agitated)%(threaten)WHAT!? %(wings)WERE THEY BIGGER THAN ME!?%(yesno('bigger than me'))"
		}
		else
		{
			"oh good I would hate to have dragonly competition"
		}
	}
	elseif reference1 == "bigger than me"
	{
		if reference0 == "yes"
		{
			"\0\b[0]%(agitated)%(threaten)%(wings)WHAT!? NO NO NO! BITE BITE GRRR CLAW BITE THEM!!! MINE MINE MINE- \n\n... \n\n\0%(agitated)%(threaten)grrrm... %(neutral)%(tired)\w8\w8\w8\w8%(neutral)%(serious)Apologies. I... %(neutral)%(nervous)got carried away."
		}
		else
		{
			"\0\b[4]%(neutral)%(serious)Ah. %(neutral)%(processing)I see. %(neutral)%(smile)That's fine then. \n\n%(narration)%(italic)...\w8 He seems happy about that,\w4 for some reason... %(italicoff)"
		}
	}
	elseif reference1 == "caught stealing"
	{
		if reference0 == "yes"
		{
			"Hmmmmmm my ad said thieves need not apply"
		}
		else
		{
			GenericPositive
		}
	}
	
	//Generic responses
	elseif reference0 == "yes"
	{
		if reference1 == "yes"; GenericPositive
		elseif reference1 == "no"; GenericNegative
		else; GenericNeutral
	}
	else //no
	{
		if reference1 == "no"; GenericPositive
		elseif reference1 == "yes"; GenericNegative
		else; GenericNeutral
	}
}

GenericNeutral
{
	"I see I see..."
}

GenericPositive
{
	"Oh good good good good"
}

GenericNegative
{
	"Hmm, this concerning..."
}

//Boss's response to the user asking a question
OnAskQuestion
{
	_input = TOLOWER(reference0)
	_intents = ProcessQuestion(_input)
	
	//The top line here and the line at the bottom can be commented if you want to get rid of the debug info
	"\_q\0\b[0]You asked: %(reference0)\n\nOutput: %(ProcessQuestion(reference0))\n\n"
	--
	if Intent("soft,growl",_intents)
	{
		"oh is that a threat? hmmmmmmmm"
	}
	elseif Intent("screaming,loud",_intents)
	{
		"AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA WHY ARE WE SCREAMING AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA"
	}
	elseif "gold" _in_ _input
	{
		"gold!!! my gold"
	}
	elseif _input == ""
	{
		"oh, uh, nothing to say, oh no"
	}
	else //Didn't match any intents
	{
		"Mysterious..."
		"Um..... yes. Yes? Yes."
	}
	--
	"\n\n%(REPLACE(OnAskBossAQuestion,'\_q',''))"
}

OnUserInputCancel
{
	if reference1 == "timeout"
	{
		"Ok, we'll come back to that"
	}
	else //closed deliberately
	{
		"Hmmm refusing to answer..."
	}
}

