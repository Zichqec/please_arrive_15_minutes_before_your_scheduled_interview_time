//Separate from the menus because you could put other interactions here, like poking or booping or punching or asking about various hotspots
OnMouseDoubleClick
{
	if reference5 == 0 //reference5 is the mouse button being clicked, in this case a left click
	{
		//reference3 is which character is being clicked
		//If you want to add double click interactions, reference4 will be the name of the hitbox the mouse is over
		if reference4 != "switch"; OnSakuraMenu
		
	}
}

OnMouseClick
{
	if reference4 == "switch"; OnFlipSwitch
}

OnFlipSwitch
{
	if HasToggledLights
	{
		if LightsOn; "\0\![bind,Lights,Off,1]%(neutral)%(narration)%(italic)Bink!%(italicoff)"
		else; "\0\![bind,Lights,Off,0]%(narration)%(italic)Bink!%(italicoff)"
	}
	else
	{
		HasToggledLights = 1
		"\t\0\b[0]%(neutral)Oh! Yes, yes, right, right. You have weak eyes. I forgot, sorry, sorry.\w8\w8\w8\w8\![bind,Lights,Off,0]"
	}
}

OnMouseMove
{
	if reference4 == "head"
	{
		stroke++
		
		if stroke % 40 == 0
		{
			OnMouseMove.Sakura
		}
	}
}

OnMouseMove.Sakura
{
	if reference4 == "head"
	{
		"\0\b[0]\s[0]Petting??"
	}
}


OnMouseLeaveAll
{
	stroke = 0
}