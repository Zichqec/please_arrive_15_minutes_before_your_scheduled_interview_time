//Separate from the menus because you could put other interactions here, like poking or booping or punching or asking about various hotspots
OnMouseDoubleClick
{
	if reference5 == 0 //reference5 is the mouse button being clicked, in this case a left click
	{
		//reference3 is which character is being clicked
		//If you want to add double click interactions, reference4 will be the name of the hitbox the mouse is over
		if reference4 != "switch"; OnSakuraMenu
		
	}
}

OnMouseClick
{
	if reference4 == "switch"; OnFlipSwitch
	elseif reference4 == "paper"; OnPushPaper
}

OnFlipSwitch
{
	if HasToggledLights
	{
		if LightsOn; "\0\![bind,Lights,Off,1]\s[%(SHIORI3FW.LastSurface[0])]\i[%(LastInterval)]%(narration)%(italic)Bink!%(italicoff)"
		else; "\0\![bind,Lights,Off,0]%(narration)%(italic)Bink!%(italicoff)"
	}
	else
	{
		HasToggledLights = 1
		"\t\0\b[0]%(neutral)Oh! Yes, yes, right, right. You have weak eyes. I forgot, sorry, sorry.\w8\w8\w8\w8\![bind,Lights,Off,0]"
	}
}

OnPushPaper
{
	"\0\b[0]\i[4]\t%(narration)%(italic)Whoosh%(italicoff)\w8\w8\w8\w8\n\n%(neutral)\![anim,clear,2]%(look)Papers... %(neutral)\![anim,clear,2]%(nervous)One moment, please. \s[700]\_w[5000]\n\n%(neutral)%(forward)Apologies. Where were we?"
}

OnMouseMove
{
	if reference4 == "head"
	{
		stroke++
		
		if stroke % 40 == 0
		{
			OnMouseMove.Sakura
		}
	}
}

OnMouseMove.Sakura : nonoverlap_pool
{
	if reference4 == "head"
	{
		"\0\b[4]%(agitated)%(pet)Rrr?"
		"\0\b[4]%(agitated)%(pet)Mrrp~"
		"\0\b[4]%(agitated)%(pet)Scritch scratch~"
		"\0\b[4]%(agitated)%(pet)Pet pet pet~"
		"\0\b[4]%(agitated)%(pet)Ohh...!"
		"\0\b[4]%(agitated)%(pet)Yes, yes..."
		"\0\b[4]%(agitated)%(pet)%(DragonNoisePrefix) %(good) pets for %(good) Boss~"
		"\0\b[4]%(agitated)%(pet)Pets... %(good)..."
	}
}


OnMouseLeaveAll
{
	stroke = 0
}