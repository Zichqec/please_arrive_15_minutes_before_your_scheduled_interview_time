OnFirstBoot
{
	//If you add variables, make sure to initialize them here!
	//Note - I haven't added anything about username or pronouns here because it's all handled by the SSP prefs
	stroke = 0
	LastTalk = ""
	BalloonColors = (1,1,1,1,1,1,0,0,0,0)
	NameIndex = 7
	CurrentName = TempNames
	NameDiscovered = 0
	SavedUsername = username
	
	"\1\s[-1]\0\s[0]Hello, and welcome to the Simplicity Template, %(username).\w8 As the name implies, this template is very simple.\w8 You will notice there is not much here.\w8 That's on purpose!\w8 This template's primary goal is to make ghost creation less intimidating.\w8 You really need almost nothing to make a ghost;\w4 a basic shell with some interesting randomtalk can be an incredible and fun experience.\w8 So all those boring functions to do with email checking and such?\w8 Not here, unless you decide you want to add them.\w8 Don't let tedious functions tire you out before you can finish your ghost!\w8 Make something simple and fun, or start small and expand upon it later!\w8 Embrace the freedom of simplicity!"
}

OnHelpWanted : all
{
	"\_b[helpwanted.png,6,6,opaque]\x"
	"\![raise,OnJobTime]"
}

OnJobTime : all
{
	"\0\b[2]%(noboss)%(narration)%(italic)The directions on the odd advertisement have led you to the base of a mountain.\w8 There's a path winding up towards...\w8 a cave?\w8\w8 It's not your first choice of location,\w4 but a job's a job.\w8%(italicoff)\n\n%(narration)%(italic)Knowing it would take you a ways away from town,\w4 you had set out early to make it on time...\w8 preferably without getting lost.\w8 %(italicoff)\n\n%(narration)%(italic)It pays off when you reach the end of the path,\w4 arriving at the mouth of a cave 20 minutes before your interview was scheduled.\w8%(italicoff)\n\n%(narration)%(italic)After taking a moment to catch your breath and straighten your clothes,\w4 you head inside...\w8\w8\w8\x"
	"\0\b[4]%(darkboss)%(narration)%(italic)...\w8 It's dark.\w8 Very dark.\w8 You don't see much when you look around,\w4 exceptâ€”\w8\w8%(italicoff)\n\n[half]AH!\x"
	"\0\b[0]%(neutralforward)Oh, grrm, you must be- %(tieprocessing)must be... %(speaksmile)my %(TempNames)! \n\n%(neutralbehind)... What time now? \w8\w8%(neutralforward)Oh! \n\n%(neutralserious)You early. I said be here 15 minutes before your interview, you here 20 minutes before... %(speakhappy)Must be very excited to start! \x"
	"\0\b[2]%(neutralforward)Good, good, we'll get right into it. \n\n%(speaklook)Now, your resume... where... Ah, here! \n\n%(paperread)Yes, yes, yes, good, good... %(papersquint)Grrm... hmm! %(paperscowl)Everything looks in order... \n\n%(papersquint)... %(TempNames). \n\n%(papernervous)I have couple quick questions to ask! And, %(neutralforward)if you have questions, you ask, too. Okay? Okay.\x"
	"\0\b[0]%(speakforward)First, firstly, first! %(input)How did you find my limited edition Help Wanted pro-\w4mot-\w4ional adver-\w4tis-\w4ing poster for job gaining?\x"
}

OnInitialize
{
	//If you want some variables to reset on boot, put them here. This event will happen even if you add other events like OnGhostChanged
	//OnInitialize is a NOTIFY event! That means it cannot return scripts. This event is only meant for things like variable changes. See this page on Ukadoc for more info: https://ukagakadreamteam.github.io/ukadoc/manual/spec_shiori3.html
	
	if !ISVAR("BalloonColors"); BalloonColors = (1,1,1,1,1,1,0,0,0,0)
	if !ISVAR("NameIndex"); NameIndex = 7
	if !ISVAR("NameDiscovered"); NameDiscovered = 0
	if !ISVAR("CurrentName"); CurrentName = TempNames
	if !ISVAR("SavedUsername"); SavedUsername = username
	
	if ISVAR("SavedChain")
	{
		CHAIN.IDName = SavedChain[0]
		CHAIN.Script = SavedChain[1]
		CHAIN.Index  = SavedChain[2]
		CHAIN.StartTime = SavedChain[3]
		ERASEVAR("SavedChain")
	}
	
	if reference0 != "reload" //For things you want to reset EXCEPT when just reloading; a convenience for you the dev, basically
	{
		LastTalk = ""
	}
}

OnDestroy
{
	if CHAIN.IDName != ""
	{
		SavedChain = IARRAY
		SavedChain[0] = CHAIN.IDName
		SavedChain[1] = CHAIN.Script
		SavedChain[2] = CHAIN.Index
		SavedChain[3] = CHAIN.StartTime
	}
}

OnBoot
{
	_temp = SPLIT(SHIORI3FW.UserBirthday,":")
	_user_birthday = "%(_temp[1])/%(_temp[2])" //M/D, if you want it D/M you can switch it around
	_today = "%(_month)/%(_day)" //Again, switch this around if using D/M
	
	"\1\s[-1]\0\b[0]\s[0]"
	--
	"\![bind,Tie,,0]\![bind,Tie,%(ANY(TiesAvailable)),1]"
	--
	if _today == _user_birthday
	{
		"\0\s[0]It is %(username)'s birthday today.\w8 Happy birthday, %(username).\w8 Developer,\w4 you can add an interesting message here.\w8 It can be as long as you like.\w8 Or, you can remove it entirely."
	}
	elseif _today == "1/1" //You can add whatever cool dates you want with more elseifs here
	{
		"\0\s[0]It's new year's day.\w8 Happy %(year),\w4 %(username)."
	}
	else //Normal boot
	{
		"\0\s[0]Hello %(username).\w8 Good to see you."
		"\0\s[0]Hello.\w8 Ready to make something cool?"
	}
}

OnClose
{
	"\0\b[0]\s[0]Farewell."
	"\0\b[0]\s[0]Goodbye."
	--
	"\w8\w8\-" //This is what makes the ghost close (after a short pause), do not remove the \- , and MAKE SURE you don't use a \e before this point or it won't work
}